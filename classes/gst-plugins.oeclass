addhook configure_use_flags to post_recipe_parse first before set_useflags
def configure_use_flags(d):
    for flag in (d.get("CONFIGURE_USE_FLAGS") or "").split():
        flag = dict(item.split("=") for item in flag.split(":"))
        if not flag.has_key('use'):
            continue

        use = flag['use']

        if 'conf' in flag:
            conf = flag['conf']
        else:
            conf = use

        d.append("RECIPE_FLAGS", use, " ")
        d.set("DEFAULT_USE_" + use, "0")

        d.set("%s_EXTRA_OECONF"%(use.upper()),
              " --disable-" + conf)
        d.set_override("%s_EXTRA_OECONF"%(use.upper()),
                       ("","USE_" + use),
                       " --enable-" + conf)
        d.append("EXTRA_OECONF","${%s_EXTRA_OECONF}"%(use.upper()))

        if 'autolib' in flag:
            d.set_override("AUTO_PACKAGE_LIBS", (">","USE_" + use),
                           flag['autolib'])

        if 'depend' in flag:
            d.set_override("DEPENDS",
                           (">","USE_" + use),
                           flag['depend'])

        if 'rdepend' in flag:
            d.set_override("RDEPENDS_${PN}-${AUTO_PACKAGE_LIBS_PKGPREFIX}" + use,
                           ("","USE_" + use),
                           flag['rdepend'])

DEPENDS =+ "libgstreamer \
native:bison \
native:flex \
native:glib-utils \
native:orc \
liborc liborc-test \
gettext \
"
FILES_${PN}-doc += "${datadir}/gtk-doc/"
RDEPENDS_${PN} += "libgstreamer"

SRC_URI += "http://gstreamer.freedesktop.org/src/${PN}/${PN}-${PV}.tar.bz2"

EXTRA_OECONF += "--x-includes=${STAGE_DIR}/machine/usr/include --x-libraries=${STAGE_DIR}/machine/usr/lib "

acpaths += "-I ${S}/common/m4 -I ${S}/m4"

AUTO_PACKAGE_LIBS_LIBDIR = "${libdir}/gstreamer-${LIBV}:libgst::.so:.la,.a"
AUTO_PACKAGE_LIBS_PROVIDEPREFIX="gst-plugin-"
LIBV ?= "0.10"
FILES_${PN}-dbg += "${libdir}/gstreamer-${LIBV}/.debug"
